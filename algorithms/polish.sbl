/* Based on the Polish stemming algorithm developed by Błażej Kubiński,
 * which can be found at https://github.com/Tutanchamon/pl_stemmer.
 *
 * The first Snowball implementation was written by Tomasz Łysiak, rewritten
 * and modified by Dmitry Shachnev.
 */

stringescapes {}

stringdef ak '{U+0105}' // ą a + ogonek
stringdef ek '{U+0119}' // ę e + ogonek
stringdef l/ '{U+0142}' // ł l + stroke
stringdef s' '{U+015b}' // ś s + acute (kreska)
stringdef c' '{U+0107}' // ć c + acute (kreska)
stringdef o' '{U+00f3}' // ó o + acute (kreska)


externals (stem)


routines (
  remove_nouns
  remove_diminutive
  remove_superlative_ends
  remove_adjective_ends
  remove_verbs_ends
  remove_adverbs_ends
  remove_plural_forms
  remove_general_ends
)


backwardmode (
  define remove_nouns as (
    (
      $(len > 6)
      [substring] among (
        // Borrowed words that match English -ation, e.g. fortyfikacja (fortification).
        'acja'     // nominative
        'acji'     // genitive/dative/locative
        'acj{ak}'  // accusative
        // Verbal nouns, e.g. czytanie, narodzenie.
        'anie' 'enie'  // nominative
        'aniu' 'eniu'  // dative/locative
          (delete)
        // Borrowed words that match English -tics, e.g. lingwistyka (linguistics).
        'tyka'
          (<- 'ty')
      )
    ) or (
      $(len > 5)
      [substring] among (
        'ach'  // plural locative
        'ami'  // plural instrumental
        'nia'  // genitive of words ending with -nie, e.g. czytanie - czytania
        'niu'  // dative/locative of words ending with -nie, e.g. czytanie - czytaniu
        'cia'  // genitive of words ending with -cie, e.g. ukrycie - ukrycia
        'ciu'  // dative/locative of words ending with -cie, e.g. ukrycie - ukryciu
        'ce'   // dative/locative of words ending with -ka, e.g. muzyka - muzyce
          (delete)
        // Borrowed words that match English -tion, e.g. intencja (intention).
        'cja'     // nominative
        'cji'     // genitive/dative/locative
        'cj{ak}'  // accusative
          (<- 'c')
      )
    )
  )

  define remove_diminutive as (
    (
      $(len > 6)
      [substring] among (
        'eczek'  // e.g. człowieczek (diminutive of człowiek)
        'iczek'  // e.g. czajniczek (diminutive of czajnik)
        'iszek'  // e.g. braciszek (diminutive of brat)
        'aszek'  // e.g. młodzieniaszek (diminutive of młodzieniec)
        'uszek'  // e.g. kwiatuszek (diminutive of kwiat)
          (delete)
      )
    ) or (
      $(len > 4)
      [substring] among (
        'ek'  // e.g. ogródek (diminutive of ogród)
        'ak'  // e.g. gęsiak (diminutive of gęś)
          (delete)
      )
    )
  )

  define remove_adjective_ends as (
    (
      $(len > 6)
      [substring] among (
        // -cz- is part of the root, -n- is a d-suffix, -y is nominative ending
        // e.g. słoneczny (sunny, from słońce)
        'czny'
          (delete)
      )
    ) or (
      $(len > 5)
      [substring] among (
        'owy' 'owa' 'owe'  // E.g. planowy (masculine), planowa (feminine), planowe (neuter)
        'ych'  // genitive/accusative/locative plural
        'ego'  // genitive/accusative singular (masculine)
        'emu'  // dative singular (masculine/neuter)
        'ej'   // genitive/dative/locative singular (feminine)
          (delete)
      )
    )
  )

  define remove_verbs_ends as (
    (
      $(len > 5)
      [substring] among (
        'bym'        // conditional 1st person singular, e.g. czytał(a)bym
                     // TODO: maybe also add 'by{s'}' for 2nd person?
        'esz'        // present 2nd person singular, e.g. piszesz
        'asz'        // present 2nd person singular, e.g. czytasz
        'cie'        // present 2nd person plural, e.g. czytacie
        'e{s'}{c'}'  // infinitive, e.g. przynieść
        'a{s'}c'     // TODO: should it be 'a{s'}{c'}'? E.g. popaść (also infinitive)
        '{l/}em'     // past 1st person singular masculine, e.g. czytałem
        'emy'        // present 1st person plural, e.g. piszemy
        'amy'        // present 1st person plural, e.g. czytamy
          (delete)
      )
    ) or (
      $(len > 3)
      [substring] among (
        'a{c'}'  // infinitive, e.g. czytać
        'e{c'}'  // infinitive, e.g. chcieć
        'i{c'}'  // infinitive, e.g. wozić
        'em'     // past 1st person singular masculine, e.g. czytałem
        'am'     // past 1st person singular feminine, e.g. czytałam
        'a{l/}'  // past 3rd person singular masculine, e.g. czytał
        'i{l/}'  // past 3rd person singular masculine, e.g. woził
        '{ak}c'  // contemporary adverbial participle (transgressive), e.g. czytając
        // different forms of active adjectival participle, e.g. czytający (one who is reading)
        '{ak}cy'     // nominative singular (masculine)
        '{ak}cego'   // genitive singular (masculine/neuter)
        '{ak}cemu'   // dative singular (masculine)
        '{ak}cym'    // instrumental/locative singular (masculine)
        '{ak}ca'     // nominative singular (feminine)
        '{ak}cej'    // genitive/dative/locative singular (feminine)
        '{ak}c{ak}'  // accusative singular (feminine)
        '{ak}ce'     // nominative/accusative singular (neuter)
        '{ak}cych'   // genitive plural
        '{ak}cymi'   // instrumental plural
          (delete)
        'aj'     // imperative, e.g. czytaj
          (<- 'a')
      )
    )
  )

  define remove_adverbs_ends as (
    (
      $(len > 4)
      [substring] among (
        'nie'  // e.g. dokładnie
          (<- 'n')
        'wie'  // e.g. możliwie
          (<- 'w')
        'rze'  // e.g. dobrze
          (<- 'r')
      )
    )
  )

  define remove_plural_forms as (
    (
      $(len > 4)
      [substring] among (
        '{o'}w'  // genitive plural, e.g. włos - włosów
        'om'     // dative plural, e.g. włos - włosom
        'ami'    // instrumental plural, e.g. włos - włosami
          (delete)
      )
    )
  )

  define remove_general_ends as (
    (
      $(len > 4)
      [substring] among (
        // These vowel endings can appear in different contexts
        'ia'    // e.g. słonia (genitive singular of słoń)
        'ie'    // e.g. słonie (nominative plural of słoń)
        'u'     // e.g. stołu (genitive singular of stół)
        '{ak}'  // e.g. książką (noun, instrumental) or piszą (verb, present 3rd person plural)
        '{ek}'  // e.g. książkę (noun, accusative) or piszę (verb, present 1st person singular)
        'i'     // e.g. książki (nominative plural of książka)
        'a'     // e.g. kobieta (nominative singular)
        'y'     // e.g. kobiety (nominative plural of kobieta)
        '{l/}'  // e.g. napadł (past 3rd person singular masculine of napaść)
          (delete)
      )
    )
  )
)

define remove_superlative_ends as (
  test (
    ['naj'] (
      backwards (
        [substring] among (
          'sze'    // adverb, e.g. w najlepsze
          'szy'    // nominative singular (masculine), e.g. najlepszy
          'sza'    // nominative singular (feminine), e.g. najlepsza
          'sz{ak}' // accusative singular (feminine), e.g. najlepszą
          'szych'  // genitive plural, e.g. najlepszych
          'szego'  // genitive/accusative singular (masculine), e.g. najlepszego
          'szemu'  // dative singular (masculine/neuter), e.g. najlepszemu
          'szym'   // instrumental/locative singular (masculine/neuter), e.g. najlepszym
          'szej'   // genitive/dative/locative singular (feminine), e.g. najlepszej
            (delete)
        )
      )
    )
  )
  [tomark sizeof 'naj'] delete
)

define stem as (
  backwards (
    do remove_nouns
    do remove_diminutive
  )
  remove_superlative_ends or (backwards do remove_adjective_ends)
  backwards (
    do remove_verbs_ends
    do remove_adverbs_ends
    do remove_plural_forms
    do remove_general_ends
  )
)

