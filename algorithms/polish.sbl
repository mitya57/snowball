/* Based on the Polish stemming algorithm developed by Błażej Kubiński,
 * which can be found at https://github.com/Tutanchamon/pl_stemmer.
 *
 * The first Snowball implementation was written by Tomasz Łysiak, rewritten
 * and modified by Dmitry Shachnev.
 */

stringescapes {}

stringdef ak '{U+0105}' // ą a + ogonek
stringdef ek '{U+0119}' // ę e + ogonek
stringdef l/ '{U+0142}' // ł l + stroke
stringdef s' '{U+015b}' // ś s + acute (kreska)
stringdef c' '{U+0107}' // ć c + acute (kreska)
stringdef o' '{U+00f3}' // ó o + acute (kreska)


externals (stem)


routines (
  remove_nouns
  remove_comparative
  remove_superlative
  remove_adjective_ends
  remove_adjective_suffices
  remove_verbs_ends
  remove_adverbs_ends
)


backwardmode (
  define remove_nouns as (
    [substring] among (
      'a' 'u' 'owi' 'iem' '{o'}w'  // masculine endings
      'i' 'e' '{ek}' '{ak}' 'om'   // feminine endings
      'o' 'em'                     // neuter endings
      'am' 'ami' 'ach'             // plural endings
      (
        delete
        ['ani'] (delete)
      )
    )
  )

  define remove_adjective_ends as (
    [substring] among (
      'y' 'e' 'ego' 'emu' 'ym'  // masculine/neuter declension
      'a' 'ej' '{ak}'           // feminine declension
      'i' 'ych' 'ymi'           // plural declension
        (delete)
    )
  )

  define remove_comparative as (
    [substring] among (
      'iejsz' 'sz'
        (delete)
    )
  )

  define remove_adjective_suffices as (
    [substring] among (
      'n' 'ow'          // common d-suffices
      'aj{ak}c' '{ak}c' // active participles
      'w'               // passive participle (TODO: explanation, tests)
        (delete)
    )
  )

  define remove_verbs_ends as (
    // TODO: More tests
    do ([substring] among (
      'bym'
      'by{s'}'
      'by{s'}my'
      'by{s'}cie'
      'by'
        (delete)
    ))
    ([substring] among (
      'a{c'}'
      'am'
      'asz'
      'amy'
      'acie'
      'aj{ak}'
      'a{l/}em'
      'a{l/}am'
      'a{l/}e{s'}'
      'a{l/}a{s'}'
      'a{l/}'
      'a{l/}a'
      'a{l/}o'
      'ali'
      'a{l/}y'
      'aj'
      'i{c'}'
      'z{ek}'
      'isz'
      'imy'
      'icie'
      'i{l/}em'
      'i{l/}am'
      'i{l/}e{s'}'
      'i{l/}a{s'}'
      'i{l/}'
      'i{l/}a'
      'i{l/}o'
      'ili'
      'i{l/}y'
        (delete)
    ))
  )

  define remove_adverbs_ends as (
    (
      $(len > 4)
      [substring] among (
        'nie'  // e.g. dokładnie
          (<- 'n')
        'wie'  // e.g. możliwie
          (<- 'w')
        'rze'  // e.g. dobrze
          (<- 'r')
      )
    )
  )
)

define remove_superlative as (
  ['naj'] delete
)

define stem as (
  not (backwards remove_verbs_ends)
  not (backwards remove_adjective_ends (
    do (
      (backwards remove_comparative)
      (do remove_superlative)
    )
    (backwards remove_adjective_suffices)
  ))
  not (backwards remove_nouns)
  not (backwards remove_adverbs_ends)
)
